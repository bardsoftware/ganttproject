apply plugin: 'java'
apply plugin: 'idea'

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
        }
    }
}

dependencies {
    providedCompile project(path: ':ganttproject', configuration: 'exported')
    compile('net.sf.mpxj:mpxj:7.8.+') {
        exclude group: 'org.xerial', module: 'sqlite-jdbc'
        exclude group: 'com.jgoodies'
    }
    compile 'javax.xml.bind:jaxb-api:2.3.+'
    compile 'javax.xml:jaxb-impl:2.1'

    compile configurations.providedCompile
    compile fileTree(dir: project.ext.libDir, include: ['*.jar'])
}

task copyPlugin(dependsOn: assemble) {
    doLast {
        println ">>> Installing $project.name"
        copy {
            into(new File(rootProject.pluginsDir, project.name))
            from(jar.outputs.getFiles().getFiles().flatten())
            from(fileTree(".")) {
                include "plugin.xml"
                include "lib/**"
            }
        }
        copy {
            into(new File(rootProject.pluginsDir, "${project.name}/lib/"))
            from(configurations.compileClasspath.minus(configurations.providedCompile.resolve())) {
                include "*.jar"
            }
        }
        println "<<< $project.name"
    }
}
