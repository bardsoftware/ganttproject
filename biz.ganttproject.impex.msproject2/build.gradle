buildscript {
    ext.kotlin_version = '1.6.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

configurations {
    api
}

dependencies {
    providedCompile project(path: ':ganttproject', configuration: 'exported')
    api('net.sf.mpxj:mpxj:9.+') {
        exclude group: 'org.xerial', module: 'sqlite-jdbc'
        exclude group: 'com.jgoodies'
    }
    implementation configurations.api
    implementation 'javax.xml.bind:jaxb-api:2.3.+'
    implementation 'javax.xml:jaxb-impl:2.1'

    implementation configurations.providedCompile
    implementation fileTree(dir: project.ext.libDir, include: ['*.jar'])
}

task copyPlugin(dependsOn: assemble) {
    doLast {
        println ">>> Installing $project.name"
        copy {
            into(new File(rootProject.pluginsDir, project.name))
            from(fileTree(".")) {
                include "plugin.xml"
            }
        }
        copy {
            into(new File(rootProject.pluginsDir, "${project.name}/lib/"))
            from(jar.outputs.getFiles().getFiles().flatten())
            from(configurations.compileClasspath.minus(configurations.providedCompile.resolve())) {
                include "*.jar"
            }
            rename { filename -> filename + ".lib" }
        }
        println "<<< $project.name"
    }
}
