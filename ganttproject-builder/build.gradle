
// Path to launch4j binary
ext.launch4j = '/opt/launch4j/launch4j'
// Directories where we build the distro
ext.distBinDir = file('dist-bin')

archivesBaseName = 'ganttproject'
version = "${rootProject.version}"

ant.importBuild 'build.xml'
ant.properties.distBin = 'dist-bin'
ant.properties.distDebWork = "deb"
ant.properties.distDebPackage = "${rootProject.buildDir}/distributions"
ant.properties.version = "${rootProject.version}"
ant.properties.version_build = "${rootProject.version}"


def devNull = new OutputStream() {
    @Override
    public void write(int b) {}
}

clean {
    delete += "dist-bin"
}

task distBin(dependsOn: [
        ':biz.ganttproject.app.libs:copyPlugin',
        ':biz.ganttproject.app.localization:copyPlugin',
        ':biz.ganttproject.core:copyPlugin',
        ':ganttproject:copyPlugin',
        ':biz.ganttproject.impex.ical:copyPlugin',
        ':biz.ganttproject.impex.msproject2:copyPlugin',
        ':org.ganttproject.chart.pert:copyPlugin',
        ':org.ganttproject.impex.htmlpdf:copyPlugin'
    ]) {
    println "===== Building GanttProject ${version} ====="
    println "===== Building generic binary distro ====="
    doLast {
        copy {
            into(rootProject.distBinDir)
            from(fileTree(".")) {
                include "lib/javagalician.jar"
                include "lib/slf4j*.jar"
                include "ganttproject.bat"
                include "ganttproject.exe"
                include "ganttproject.l4j.ini"
                include "HouseBuildingSample.gan"
                include "LICENSE"
                include "logback.xml"
            }
        }
        copy {
            into(rootProject.distBinDir)
            from(fileTree(".")) {
                include "ganttproject"
                include "ganttproject.command"
            }
            fileMode 0755
        }
        copy {
            into(rootProject.pluginsDir)
            from(fileTree(".")) {
                include "VERSION"
            }
        }
    }
}

tasks.register('distZip', Zip) {
    archiveFileName = "$archivesBaseName-${version}.zip"
    destinationDirectory = new File(rootProject.buildDir, "distributions")
    from 'dist-bin'
}
tasks.distZip.dependsOn distBin

task distWin(dependsOn: distBin) {
    doLast {
        println '======== Building Windows Installer ========'
        println '= Generating NSIS script ...'
//        exec {
//            workingDir '.'
//            commandLine './gen-nsis', semver, version
//        }

//        println '= Generating ganttproject.exe with launch4j ...'
//        exec {
//            workingDir '.'
//            commandLine launch4j, 'ganttproject-launch4j.xml'
//            standardOutput = devNull
//        }
        println '= Copying required files to dist-win...'
        copy {
            from 'dist-bin'
            into 'dist-win'
            include '**'
        }
        copy {
            from '.'
            into 'dist-win'
            include "ganttproject.l4j.ini"
            include 'ganttproject.nsi.tpl'
            include "ganttproject_16.ico"
            include "ganttproject.ico"
            rename 'ganttproject.nsi.tpl', 'ganttproject.nsi'
        }
        copy {
            from '..'
            into 'dist-win'
            include 'runtime/**'
        }
//        println '= Building installer...'
//        exec {
//            workingDir 'dist-win'
//            commandLine 'makensis', 'ganttproject.nsi'
//            standardOutput = devNull
//        }
//        copy {
//            from 'dist-win'
//            into new File(buildDir, '/distributions')
//            include "ganttproject-${version}.exe"
//        }
//        println "===== Windows installer is ready in ${buildDir}/distributions ====="
    }
}

task distDebPre {
    doFirst {
        copy {
            into 'deb/usr/share/doc/ganttproject'
            from(rootProject.projectDir) {
                include "AUTHORS"
                include "CHANGELOG"
                include "README"
            }
        }
        exec {
            commandLine "gzip"
            args "-n", "-f", "deb/usr/share/doc/ganttproject/CHANGELOG"
        }
        exec {
            commandLine "mv"
            args "deb/usr/share/doc/ganttproject/CHANGELOG.gz", "deb/usr/share/doc/ganttproject/changelog.Debian.gz"
        }
        mkdir("${rootProject.buildDir}/distributions")
    }
}


task runApp(type: JavaExec, dependsOn: distBin) {
    workingDir = 'dist-bin'
    classpath = (
        fileTree(dir: 'dist-bin', includes: ['eclipsito.jar']) +
            files('dist-bin') +
            fileTree(dir: 'dist-bin/lib', includes: ['*.jar'])
    )
    main = 'com.bardsoftware.eclipsito.Launch'
    args '--verbosity', '4', '--app', 'net.sourceforge.ganttproject.GanttProject', '--version-dirs', "plugins"
    jvmArgs = ['-ea'] + javaExportOptions
}

task debugApp(type: JavaExec, dependsOn: distBin) {
    workingDir = 'dist-bin'
    classpath = (
        fileTree(dir: 'dist-bin', includes: ['eclipsito.jar']) +
            files('dist-bin') +
            fileTree(dir: 'dist-bin/lib', includes: ['*.jar'])
    )
    main = 'com.bardsoftware.eclipsito.Launch'
    args '--verbosity', '4', '--app', 'net.sourceforge.ganttproject.GanttProject', '--version-dirs', "plugins"
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,address=localhost:9009,server=y,suspend=y'] + javaExportOptions
}

tasks.getByPath('distZip')
    .mustRunAfter(':org.ganttproject.impex.htmlpdf:copyPlugin')
    .mustRunAfter(':org.ganttproject.chart.pert:copyPlugin')
    .mustRunAfter(':biz.ganttproject.impex.msproject2:copyPlugin')
    .mustRunAfter(':biz.ganttproject.impex.ical:copyPlugin')
    .mustRunAfter(':ganttproject:copyPlugin')
    .mustRunAfter(':biz.ganttproject.core:copyPlugin')
    .mustRunAfter(':biz.ganttproject.app.localization:copyPlugin')
    .mustRunAfter(':biz.ganttproject.app.libs:copyPlugin')
    .mustRunAfter('build')
