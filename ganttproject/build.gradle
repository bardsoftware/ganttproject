buildscript {
    ext.kotlin_version = '1.1.1'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.1.1"
}
apply plugin: 'java'
apply plugin: 'kotlin'

ext {
  libDir = 'lib/core'
}

dependencies {
  compile fileTree(dir: project.ext.libDir, include: ['*.jar'])
  compile project(':..:biz.ganttproject.core')
  compile files('data/resources')
  mavenDeps group: 'org.controlsfx', name: 'controlsfx', version: '8.40.+'
  mavenDeps group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.+'
  mavenDeps group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.+'
  mavenDeps group: 'de.jensd', name: 'fontawesomefx-fontawesome', version: '4.6.+', transitive: true
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.1"
    mavenDeps "org.jetbrains.kotlin:kotlin-stdlib:1.1.1"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

updateMavenDeps.doFirst {
  into project.ext.libDir
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDirs = ["src"]
            include "**/*.css"
        }
    }

    main.java.srcDirs += 'src'
    main.kotlin.srcDirs  += 'src'
}

task updateVersion {
    doFirst {
        def gpVersionFile = new File("${projectDir}/src/net/sourceforge/ganttproject/GPVersion.java")
        def gpversion = gpVersionFile.getText('UTF-8')
                .replaceAll('.*BUILD.NUMBER.*', "  public static String BUILD = \"${rootProject.buildNum}\"; // BUILD NUMBER")
        gpVersionFile.write(gpversion, 'UTF-8')
    }
}

task copyPluginFiles(type: Copy) {
    into(new File(rootProject.pluginsDir, project.name))
    from(jar.outputs.getFiles().getFiles().flatten())
    from(fileTree(".")) {
      include "plugin.xml"
      include "lib/core/**.jar"
      exclude "lib/core/eclipsito.jar"
      include "data/resources/**.ttf"
      include "data/resources/**.properties"
      include "data/resources/calendar/**"
      include "data/resources/icons/**"
      include "data/resources/language/**"
      include "data/resources/xslfo/**"
      include "data/resources/xslt/**"
    }
}

task copyEclipsito(type: Copy) {
    into(rootProject.distBinDir)
    from(fileTree("lib/core")) {
      include "eclipsito.jar"
    }
}

task copyPlugin(dependsOn: ['copyPluginFiles', 'copyEclipsito']) << { println "Copying $project.name to $rootProject.pluginsDir" }
